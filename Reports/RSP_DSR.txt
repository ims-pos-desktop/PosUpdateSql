CREATE OR ALTER  PROC [dbo].[RSP_DSR]
--DECLARE 
@D1 INT = 2018,
@D2 INT = 9,
@DIVISION VARCHAR(3) = '%',
@TextBoxValue DECIMAL (18,2) = 75000
AS
DECLARE @DATE1 DATETIME = DATEFROMPARTS(@D1, @D2, 1)
DECLARE @DATE2 DATETIME = EOMONTH(@DATE1)
IF OBJECT_ID('TEMPDB..#CATEGORYTOTAL') IS NOT NULL
DROP TABLE #CATEGORYTOTAL
IF OBJECT_ID('TEMPDB..#OVERALLTOTAL') IS NOT NULL
DROP TABLE #OVERALLTOTAL
SELECT DATEPART(D, TRNDATE) D, '   ' + MCAT1 Particulars, CONVERT(DECIMAL(18,2), SUM(BILLAMOUNT)) SALES, SUM(Quantity) Quantity INTO #CATEGORYTOTAL FROM
(
SELECT TM.TRNDATE, TP.Quantity, CASE WHEN ISNULL(TPS.TAKEAWAY, 0) = 0 THEN MI.MCAT1 ELSE 'TAKEAWAY' END MCAT1, 
CASE WHEN TM.VoucherType IN ('SI', 'TI') THEN (TP.TAXABLE + TP.NONTAXABLE + TP.VAT) ELSE (TP.TAXABLE + TP.NONTAXABLE + TP.VAT) * -1 END BILLAMOUNT
FROM RMD_TRNMAIN TM JOIN RMD_TRNPROD TP ON TM.VCHRNO = TP.VCHRNO
LEFT JOIN RMD_TRNPROD_STATUS TPS ON TP.VCHRNO = TPS.VCHRNO AND TP.MCODE = TPS.MCODE
JOIN MENUITEM MI ON TP.MCODE = MI.MCODE
WHERE TM.DIVISION LIKE @DIVISION AND TM.VoucherType IN ('SI', 'TI', 'CN') AND TM.TRNDATE BETWEEN @DATE1 AND @DATE2
) A GROUP BY TRNDATE, MCAT1


SELECT DATEPART(D, TRNDATE) D, SUM(Customer_Count) PAX, CONVERT(DECIMAL(18,2), SUM(BILLAMOUNT)) TOTALSALES, COUNT(DISTINCT BILLS) BILLS,
CONVERT(DECIMAL(18,2), SUM(BILLAMOUNT) / COUNT(DISTINCT BILLS)) AVERAGEPERBILL,
CONVERT(DECIMAL(18,2), SUM(BILLAMOUNT) / SUM(Customer_Count)) AVERAGEPERPAX,
CONVERT(DECIMAL(18,2), SUM(DISCOUNT)) DISCOUNT, CONVERT(DECIMAL(18,2), SUM(CASH)) CASH,  
CONVERT(DECIMAL(18,2), SUM(CreditCard)) CreditCard,  CONVERT(DECIMAL(18,2), SUM(CREDIT)) CREDIT,
CONVERT(DECIMAL(18,2), SUM(BILLAMOUNT) * 100 / @TextBoxValue) SALESVSPLAN INTO #OVERALLTOTAL 
FROM
(
SELECT TM.TRNDATE, CASE WHEN ISNULL(TM.Customer_Count, 0) = 0 THEN 1 ELSE TM.Customer_Count END Customer_Count, 
CASE WHEN TM.VoucherType IN ('SI', 'TI') THEN TM.VCHRNO ELSE NULL END BILLS,
CASE WHEN TM.VoucherType IN ('SI', 'TI') THEN TM.NETAMNT ELSE TM.NETAMNT * -1 END BILLAMOUNT,
CASE WHEN TM.VoucherType IN ('SI', 'TI') THEN TM.DCAMNT ELSE TM.DCAMNT * -1 END DISCOUNT,
CASE WHEN (TM.TRNMODE = 'CASH') THEN CASE WHEN TM.VoucherType IN ('CN') THEN (TM.NETAMNT) *-1 ELSE TM.NETAMNT END ELSE CASE WHEN TM.TRNMODE IN ('MIXEDMODE', 'Mixed') THEN NETCASH ELSE NULL END END AS CASH,
CASE WHEN (TM.TRNMODE = 'CreditCard' OR TM.TRNMODE = 'Credit Card') THEN CASE WHEN TM.VoucherType IN ('CN', 'RE') THEN (TM.NETAMNT) *-1 ELSE TM.NETAMNT END ELSE CASE WHEN TM.TRNMODE IN ('MIXEDMODE', 'Mixed') THEN NCREDITCARD ELSE NULL END END  AS CreditCard,
CASE WHEN (TM.TRNMODE = 'CREDIT') THEN CASE WHEN TM.VoucherType IN ('CN', 'RE') THEN (TM.NETAMNT) *-1 ELSE TM.NETAMNT END ELSE CASE WHEN TM.TRNMODE IN ('MIXEDMODE', 'Mixed') THEN NCREDIT ELSE NULL END END AS CREDIT
FROM RMD_TRNMAIN TM --JOIN RMD_TRNPROD TP ON TM.VCHRNO = TP.VCHRNO
JOIN BILLTENDER BT ON TM.VCHRNO = BT.VNO
WHERE TM.DIVISION LIKE @DIVISION AND TM.VoucherType IN ('SI', 'TI', 'CN') AND TM.TRNDATE BETWEEN @DATE1 AND @DATE2
) A GROUP BY TRNDATE
ORDER BY TRNDATE

SELECT SUBSTRING(Particulars, 4, LEN(Particulars) - 3) Particular, * FROM
(
	SELECT * fROM
	(
		SELECT D, Particulars, SALES FROM #CATEGORYTOTAL 
	) A
PIVOT ( MAX(SALES) FOR D IN ([1],[2],[3],[4],[5],[6],[7],[8],[9],[10],[11],[12],[13],[14], [15], [16], [17], [18], [19], [20], [21], [22], [23], [24], [25], [26], [27], [28], [29], [30],[31])) AS TOTAL
UNION ALL
SELECT * FROM
(
	SELECT A.* fROM 
		(
			SELECT *, SUM(TOTALSALES) OVER(ORDER BY D ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW) AS [MTD SALES], 
			AVG(TOTALSALES) OVER(ORDER BY D ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW) AS [MTD AVG SALES],
			SUM(BILLS) OVER(ORDER BY D ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW) AS [MTD BILLS],
			AVG(BILLS) OVER(ORDER BY D ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW) AS [MTD AVG BILLS] FROM #OVERALLTOTAL
		) OT LEFT JOIN 
		(
			SELECT * fROM 
			(
				SELECT D, Particulars, SALES FROM #CATEGORYTOTAL 
			) A
			PIVOT ( MAX(SALES) FOR Particulars IN ([   FOOD], [   BEVERAGE])) AS TOTAL
		) CT ON OT.D = CT.D
		LEFT JOIN 
		(
			SELECT * fROM 
			(
				SELECT D, Particulars, Quantity FROM #CATEGORYTOTAL 
			) A
			PIVOT ( MAX(Quantity) FOR Particulars IN ([   FOOD], [   BEVERAGE])) AS TOTAL
		) QT ON OT.D = QT.D
		CROSS APPLY 
		( 
			VALUES 
				(OT.D, '00 Total Sales', TOTALSALES), 
				(OT.D, '01 Transaction', BILLS),
				(OT.D, '02 Average Per Transaction',AVERAGEPERBILL),				
				(OT.D, '03 Average Per Ticket',AVERAGEPERPAX),	
				(OT.D, '04 Cash Sales', CASH),
				(OT.D, '05 Credit Card', CreditCard),
				(OT.D, '06 Credit Sales', CREDIT),
				(OT.D, '07 Discount', DISCOUNT),
				(OT.D, '08 Sales vs Plan', SALESVSPLAN),
				(OT.D, '09 Food Sales(%)', CT.[   FOOD] * 100 / OT.TOTALSALES),
				(OT.D, '10 Beverage Sales(%)', CT.[   BEVERAGE] * 100 / OT.TOTALSALES),
				(OT.D, '11 MTD Total Sales', [MTD SALES]),
				(OT.D, '12 MTD Average Sales', [MTD AVG SALES]),
				(OT.D, '13 MTD Total Transaction', [MTD BILLS]),
				(OT.D, '14 MTD Average Transaction', [MTD AVG BILLS]),
				(OT.D, '15 MTD Average Sales per Transaction', [MTD SALES] / [MTD BILLS]),
				(OT.D, '16 Food Capture Ratio', qt.[   FOOD] * 100 /  OT.PAX),
				(OT.D, '17 PAX', OT.PAX)
				--(OT.D, '18 Food Capture Ratio', qt.[   FOOD])
		)
	A (D, LABEL, AMOUNT)
) A
PIVOT ( MIN(AMOUNT) FOR D IN ([1],[2],[3],[4],[5],[6],[7],[8],[9],[10],[11],[12],[13],[14], [15], [16], [17], [18], [19], [20], [21], [22], [23], [24], [25], [26], [27], [28], [29], [30],[31])) AS TOTAL
) A

DROP TABLE #CATEGORYTOTAL
DROP TABLE #OVERALLTOTAL