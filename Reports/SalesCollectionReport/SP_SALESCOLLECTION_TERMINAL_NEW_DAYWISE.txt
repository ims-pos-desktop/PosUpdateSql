CREATE OR ALTER PROCEDURE SP_SALESCOLLECTION_TERMINAL_NEW_DAYWISE
@DATE1 DATETIME,
@DATE2 DATETIME,
@div varchar(10),
@VCHR1 VARCHAR(15),
@SMODE VARCHAR(25)='%',
@TERMINAL VARCHAR(25),
@FLAG TINYINT = 0 ---0 filter by TRNDATE |1 filter by DAYSTARTDATE
AS
--DECLARE @DATE1 DATETIME='2020-07-16',@DATE2 DATETIME='2021-07-15',@div varchar(10)='%',@VCHR1 VARCHAR(15)='SR',@SMODE VARCHAR(25)='%' ,@USER VARCHAR(25)='%' ,@flag int =1,@TERMINAL VARCHAR(25)='%'

DECLARE @VCHR2 VARCHAR(25)
DECLARE @VCHR3 VARCHAR(25)
DECLARE @VDISCOUNT AS VARCHAR(25)
SET @VDISCOUNT = '%'
SET @DATE2 = @DATE2 + '23:59:59.990'

IF @VCHR1= 'SR' 
BEGIN
	SET @VCHR2 = 'CN'
	set @vchr3 = 'NR'
END

BEGIN
	IF @SMODE = 'C-Card Discount'
		BEGIN
			SET @VDISCOUNT = 'CARD DISCOUNT'
			SET @SMODE = 'CreditCard'  
		END
	ELSE
		BEGIN
			SET @VDISCOUNT = '%'	
		END
END

BEGIN
	DECLARE @PaymentCols AS NVARCHAR(MAX), 
	@PaymentNullCols NVARCHAR(MAX),
	@PaymentSumCols NVARCHAR(MAX),
    @query  AS NVARCHAR(MAX);

	SET @PaymentCols = STUFF((SELECT distinct ',' + QUOTENAME(COLUMN_NAME) FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'tblBillTender' AND COLUMN_NAME NOT IN ('VCHRNO','DIVISION','PHISCALID', 'COMPANYID','TRANSACTIONID') FOR XML PATH(''), TYPE).value('.', 'NVARCHAR(MAX)') ,1,1,'')
	SET @PaymentSumCols = STUFF((SELECT distinct ',SUM(' + QUOTENAME(COLUMN_NAME) + ')' FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'tblBillTender' AND COLUMN_NAME NOT IN ('VCHRNO','DIVISION','PHISCALID', 'COMPANYID','TRANSACTIONID') FOR XML PATH(''), TYPE).value('.', 'NVARCHAR(MAX)') ,1,1,'')
	SET @PaymentNullCols = STUFF((SELECT ', NULL' FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'tblBillTender' AND COLUMN_NAME NOT IN ('VCHRNO','DIVISION','PHISCALID', 'COMPANYID','TRANSACTIONID') FOR XML PATH(''), TYPE).value('.', 'NVARCHAR(MAX)') ,1,1,'')
	
	IF OBJECT_ID('TEMPDB..#DATA') is not null drop table #DATA

	SELECT A.SESSIONDATE, A.TRNDATE, A.BSDATE, A.VCHRNO BILLNO, A.CHALANNO, A.PARTY,
	CONVERT(NUMERIC(18,2), A.TOTAMNT * A.MULTIPLIER) GROSS,
	CONVERT(NUMERIC(18,2), A.DCAMNT * A.MULTIPLIER) DCAMNT,
	CONVERT(NUMERIC(18,2), A.STAX * A.MULTIPLIER)  STAX,
	CONVERT(NUMERIC(18,2), A.NETSALE * A.MULTIPLIER) NETSALE,
	CONVERT(NUMERIC(18,2), A.VATAMNT * A.MULTIPLIER) VATAMNT,
	CONVERT(NUMERIC(18,2), A.NETAMNT * A.MULTIPLIER) NETAMNT,
	X.*, TRNUSER, TRNTIME, TDATE, VNO,TERMINAL,A.DIVISION BRANCHID, PAX,
	A.BILLTOPAN, A.BILLTOMOB INTO #DATA
	FROM
	(
		SELECT CONVERT(DATE,D.DAYSTARTDATE) SESSIONDATE, CONVERT(VARCHAR(25),A.TRNDATE,1) AS TRNDATE,A.BSDATE, A.VCHRNO, A.CHALANNO, A.PHISCALID,
		IIF(ISNULL(A.BILLTO,'') <> '', A.BILLTO, B.ACNAME) PARTY, A.TOTAMNT, A.DCAMNT, A.STAX, A.TAXABLE + A.NONTAXABLE NETSALE, A.VATAMNT, A.NETAMNT, 
		A.TRNUSER, TRNTIME,TRNDATE AS TDATE, ISNULL(A.VNUM,A.VCHRNO) AS VNO,TERMINAL,A.DIVISION, CONVERT(INT, A.Customer_Count) PAX,
		ISNULL(A.BILLTOTEL, '') BILLTOPAN, A.BILLTOMOB, IIF(A.VoucherType IN (@VCHR1, @VCHR2, @VCHR3, 'RE'), -1, 1) Multiplier 
		FROM RMD_TRNMAIN A INNER JOIN RMD_ACLIST B ON A.TRNAC = B.ACID
		left JOIN SESSION S ON a.[SHIFT]= S.SESSIONID AND A.PHISCALID = S.PHISCALID AND A.DIVISION = S.Division
		left JOIN DAYS D ON S.DAYSTARTID = D.DAYSTARTID AND D.PHISCALID = S.PHISCALID AND D.DIVISION = S.Division
		WHERE A.VoucherType IN ('SI','TI','NC','RE',@VCHR1,@VCHR2,@VCHR3) 
		AND (CASE WHEN @flag=1 THEN D.DAYSTARTDATE ELSE TRNDATE END between @DATE1 and @DATE2) 
		and A.division like @div 
		AND A.TERMINAL LIKE @TERMINAL 
		AND A.TRNMODE LIKE @SMODE 
		AND ISNULL(A.CHOLDER,'') LIKE @VDISCOUNT 
		AND ((@SMODE = 'MEMBERSHIP' AND ISNULL(A.MEMBERNAME ,'') <> '') OR (@SMODE <> 'MEMBERSHIP' AND ISNULL(A.MEMBERNAME ,'') LIKE '%'))
	) A
	LEFT JOIN tblBillTender X ON A.VCHRNO = X.VCHRNO AND A.DIVISION = X.DIVISION AND A.PHISCALID = X.PHISCALID 

	SET @query = N'SELECT * FROM 
	(
		SELECT A.Sessiondate,A.TRNDATE, A.BSDATE, A.BILLNO, A.CHALANNO, A.PARTY, GROSS, DCAMNT, STAX, NETSALE, VATAMNT, NETAMNT, '+@PaymentCols+', TRNUSER, TRNTIME, TDATE, VNO, TERMINAL, A.BRANCHID, PAX, ''B'' FLG FROM #DATA A
		UNION ALL
		SELECT NULL, NULL, NULL, NULL, NULL, ''TOTAL'', SUM(GROSS), SUM(DCAMNT), SUM(STAX), SUM(NETSALE), SUM(VATAMNT), SUM(NETAMNT), '+ @PaymentSumCols +', NULL, NULL, NULL, NULL, TERMINAL, NULL, SUM(PAX), ''C'' FLG FROM #DATA A GROUP BY TERMINAL
		UNION ALL
		SELECT NULL, TNAME, NULL, NULL, NULL, NULL,NULL,NULL,NULL,NULL,NULL,NULL,'+@PaymentNullCols+', NULL, NULL,NULL,NULL,TERMINAL, NULL, NULL, ''A'' FLG FROM
		(
			SELECT DISTINCT B.NAME TNAME, A.TERMINAL FROM #DATA A JOIN SALESTERMINAL B ON A.TERMINAL = B.INITIAL  
		) A
		UNION ALL
		SELECT NULL,NULL, NULL, NULL, NULL, ''GRAND TOTAL'', SUM(GROSS), SUM(DCAMNT), SUM(STAX), SUM(NETSALE), SUM(VATAMNT), SUM(NETAMNT), '+ @PaymentSumCols +', NULL, NULL, NULL, NULL, ''ZZZZZZ'', NULL, SUM(PAX), ''D'' FLG FROM #DATA A
    ) M  ORDER BY  M.TERMINAL, M.FLG,CAST(M.TDATE AS DATETIME), left(M.VNO,2) desc,CAST(RIGHT(VNO,len(VNO)-2) as numeric)
	'
	--PRINT @QUERY
	EXECUTE(@query)	
END
