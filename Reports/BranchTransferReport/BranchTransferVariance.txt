CREATE OR ALTER PROC RSP_BranchTransferVariance
--DECLARE 
@FROM_BRANCH VARCHAR(25) = '%',
@TO_BRANCH VARCHAR(3) = '%',
@FYID VARCHAR(10),
@CHK_ShowVarianceOnly TINYINT = 0,			--CHK_ShowVarianceOnly:1:0
@OPT_SHOWSUMMARY_REPORT TINYINT = 1             --Summary:1,Detail:0
AS
DECLARE @SUMMARY TINYINT = @OPT_SHOWSUMMARY_REPORT
SELECT O.VCHRNO OUT_VOUCHER, I.VCHRNO IN_VOUCHER, COALESCE(O.MCODE, I.MCODE) MCODE, M.MENUCODE, M.DESCA, 
O.OutQty, CONVERT(NUMERIC(18,2), O.OutValue) OutValue, 
I.InQty, CONVERT(NUMERIC(18,2),I.InValue) InValue, 
COALESCE(O.OutQty,0) - COALESCE(I.InQty,0) QtyShort, CONVERT(NUMERIC(18,2), COALESCE(O.OutValue,0) - COALESCE(I.InValue,0)) ValueShort
FROM
(
	SELECT TBO.VCHRNO, TBO.TRNDATE, TBO.BSDATE, TBO.TRNTIME, TBO.TRNUSER, TBO.BILLTOADD, IIF(@SUMMARY =1,'',TPO.MCODE) MCODE, SUM(REALQTY) OutQty, SUM(AMOUNT) OutValue
	FROM TRNMAIN_BRANCH_OUT TBO JOIN TRNPROD_BRANCH_OUT TPO ON TBO.VCHRNO = TPO.VCHRNO
	WHERE TBO.BILLTO LIKE @FROM_BRANCH AND TBO.BILLTOADD LIKE @TO_BRANCH AND TBO.PhiscalID = @FYID 
	GROUP BY TBO.VCHRNO, TBO.TRNDATE, TBO.BSDATE, TBO.TRNTIME, TBO.TRNUSER, TBO.BILLTOADD, IIF(@SUMMARY =1,'',TPO.MCODE)
) O FULL OUTER JOIN
(
	SELECT TBO.VCHRNO, TBO.TRNDATE, TBO.BSDATE, TBO.TRNTIME, TBO.TRNUSER, TBO.BILLTO, TBO.REFBILL, IIF(@SUMMARY =1,'',TPO.MCODE) MCODE, SUM(REALQTY_IN) InQty, SUM(AMOUNT) InValue
	FROM RMD_TRNMAIN TBO JOIN RMD_TRNPROD TPO ON TBO.VCHRNO = TPO.VCHRNO
	WHERE TBO.VoucherType = 'TR' AND TBO.BILLTO LIKE @FROM_BRANCH AND TBO.BILLTOADD LIKE @TO_BRANCH AND TBO.PhiscalID = @FYID
	GROUP BY TBO.VCHRNO, TBO.TRNDATE, TBO.BSDATE, TBO.TRNTIME, TBO.TRNUSER, TBO.BILLTO, TBO.REFBILL, IIF(@SUMMARY =1,'',TPO.MCODE) 
) I ON O.VCHRNO = I.REFBILL AND O.MCODE = I.MCODE
LEFT JOIN MENUITEM M ON M.MCODE = COALESCE(O.MCODE, I.MCODE)
WHERE @CHK_ShowVarianceOnly = 0 OR COALESCE(O.OutQty,0) - COALESCE(I.InQty,0) <> 0 OR COALESCE(O.OutValue,0) - COALESCE(I.InValue,0) <> 0