CREATE OR ALTER PROC [dbo].[RSP_PartyVsProductSalesReport]
--DECLARE
@DATE1 DATE,
@DATE2 DATE,
@DIVISION VARCHAR(3) = '%',
@CUSTOMER_ACID VARCHAR(20) ='%',
@SUPPLIER_ACID VARCHAR(20) ='%',
@AREA_ID INT  = 1,
@CATEGORY_ID INT = 0,
@MCODE varchar(25) = '%',
@MENUCAT VARCHAR(100) ='%',
@MGROUP VARCHAR(20) = '%',
@PTYPE INT = 100,
@SELECTEDITEMLIST VARCHAR(MAX) = '',
@CHK_BARCODEWISEREPORT TINYINT = 1,				--BarCodeWise:1:0
@CHK_BARCODEDETAIL TINYINT =1,					--BarCodeDetail:1:0
@CHK_BATCHWISE TINYINT = 0,                     --BatchWise:1:0
@OPT_SHOWSUMMARY_REPORT TINYINT = 1									--Summary:1,Detail:0

AS

--SELECT @DATE1 = '2023-09-10', @DATE2 = '2024-03-01', @DIVISION = '%'


IF ISNULL(@SELECTEDITEMLIST,'') = '' AND @MCODE <> '%'
	SET @SELECTEDITEMLIST = @MCODE

SELECT M.VCHRNO, M.DIVISION, M.PhiscalID, M.TRNDATE, PA.ACID, PA.ACNAME, PA.AREA_NAME, PA.CATEGORY_NAME, M.MEMBERNO, P.MCODE, 
IIF(@CHK_BARCODEWISEREPORT = 1, P.BC,'') BC, IIF(@CHK_BATCHWISE =1, P.BATCH,'') BATCH,P.RATE,
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * P.Quantity Quantity,
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.AMOUNT) AMOUNT, 
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.DISCOUNT) DISCOUNT, 
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.SERVICETAX) SERVICETAX, 
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), (P.TAXABLE + P.NONTAXABLE)) NETSALE,
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.TAXABLE) TAXABLE, 
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.NONTAXABLE) NONTAXABLE, 
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.VAT) VAT, 
IIF(M.VoucherType IN ('SI','TI'), 1, -1) * CONVERT(NUMERIC(15,2), P.NETAMOUNT) NETAMOUNT ,
MI.MCAT ITEM_CATEGORY,  S.NAME SALESTERMINAL, M.VNUM
INTO #DATA
FROM SALES_TRNMAIN M JOIN SALES_TRNPROD P ON M.VCHRNO = P.VCHRNO
JOIN dbo.fnPartyFilter(@AREA_ID, @CATEGORY_ID, @CUSTOMER_ACID, default, default, default, default, default) PA ON PA.ACID = COALESCE(M.PARAC, M.TRNAC)
JOIN MENUITEM MI ON P.MCODE = MI.MCODE
LEFT JOIN SALESTERMINAL S ON M.TERMINAL = S.INITIAL
WHERE M.DIVISION LIKE @DIVISION AND M.TRNDATE BETWEEN @DATE1 AND @DATE2
AND M.VoucherType IN ('SI', 'TI', 'CN')

--SELECT * FROM #DATA

IF @OPT_SHOWSUMMARY_REPORT = 1
	SELECT * FROM
	(
		SELECT ACID, ACNAME, AREA_NAME, CATEGORY_NAME, M.MCODE, M.MENUCODE, M.DESCA, BC, BATCH, SUM(Quantity) Quantity, SUM(AMOUNT) AMOUNT, SUM(DISCOUNT) DISCOUNT, SUM(SERVICETAX) SERVICETAX,
		SUM(NETSALE) NETSALE, SUM(TAXABLE) TAXABLE, SUM(NONTAXABLE) NONTAXABLE, SUM(D.VAT) VAT, SUM(NETAMOUNT) NETAMOUNT , ITEM_CATEGORY , SALESTERMINAL FROM #DATA D
		JOIN dbo.fnProductFilter(@SELECTEDITEMLIST, @MENUCAT, default, @MGROUP, @PTYPE, @SUPPLIER_ACID,default, default, default, default, default, default) M ON D.MCODE = M.MCODE	
		GROUP BY ACID, ACNAME, AREA_NAME, CATEGORY_NAME, BC, BATCH, m.MCODE, M.MENUCODE, M.DESCA,ITEM_CATEGORY,SALESTERMINAL
	) A OUTER APPLY dbo.fnGetBarcodeDetail(A.MCODE, A.BC) BD
	ORDER BY ACNAME, DESCA, BC
ELSE 
	SELECT D.VCHRNO, D.TRNDATE, D.ACID, D.ACNAME, D.AREA_NAME, D.CATEGORY_NAME, M.MCODE, M.MENUCODE, M.DESCA, D.BC, D.BATCH, D.RATE, D.Quantity, D.AMOUNT, D.DISCOUNT, 
	D.SERVICETAX, D.NETSALE, D.TAXABLE, D.NONTAXABLE, D.VAT, D.NETAMOUNT, ITEM_CATEGORY , SALESTERMINAL ,  BD.* FROM #DATA D
	JOIN dbo.fnProductFilter(@SELECTEDITEMLIST, @MENUCAT, default, @MGROUP, @PTYPE, @SUPPLIER_ACID,default, default, default, default, default, default) M ON D.MCODE = M.MCODE
	OUTER APPLY dbo.fnGetBarcodeDetail(d.MCODE, D.BC) BD
	ORDER BY D.TRNDATE, SUBSTRING(D.VNUM,3, LEN(d.VNUM)-2), M.DESCA, D.BC
DROP TABLE #DATA