CREATE OR ALTER   PROC [dbo].[RSP_SHIFTCLOSEREPORT]
--DECLARE 
@DIVISION VARCHAR(3) = '%',
@DATE1 DATETIME,
@DATE2 DATETIME,
@USER VARCHAR(50) = '%',
@SALESTERMINAL VARCHAR(3) = '%'
AS
--SELECT @DATE1 = '1 NOV 2023', @DATE2 = '30 NOV 2023'

IF OBJECT_ID('TEMPDB..#RESULT') IS NOT NULL DROP TABLE #RESULT
IF OBJECT_ID('TEMPDB..#SETTLEMENT') IS NOT NULL DROP TABLE #SETTLEMENT

DECLARE @PaymentSumCols NVARCHAR(MAX), @query  AS NVARCHAR(MAX);

/*
 * Drop dummy table vwDynamicTender so  that OSP_UpdateDynamicTender can create dynamic view
 */
IF EXISTS(SELECT * FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME = 'vwDynamicTender' AND TABLE_TYPE = 'BASE TABLE')
DROP TABLE vwDynamicTender

EXEC OSP_UpdateDynamicTender

select S.SESSIONID, S.DIVISION, S.PhiscalID, S.USERID, S.SessionStartDate, S.SessionEndDate, S.SessionEndUser, S.TerminalID
, SUM(D.AMOUNT) SETTLEMENTAMOUNT, ISNULL(P.AMOUNT,0) PAYOUTAMOUNT, ISNULL(C.AMOUNT,0) CASHDROPAMOUNT, ISNULL(AC.AMOUNT,0) AdvanceCollection INTO #SETTLEMENT
FROM Session S 
LEFT JOIN tblDenomination D ON D.SESSIONID = S.SessionID AND S.DIVISION = D.DIVISION AND S.PhiscalID = D.PhiscalID
LEFT JOIN 
(
	SELECT SUM(AMOUNT) AMOUNT,DIVISION,SHIFTID,PHISCALID FROM CASHFLOWTRAN 
	GROUP BY SHIFTID,DIVISION,PhiscalID
) P ON S.Division=P.division AND S.SessionID=P.SHIFTID AND S.PhiscalID=P.PhiscalID
LEFT JOIN 
(
	SELECT SUM(AMOUNT) AMOUNT,DIVISION,SHIFTID,PHISCALID FROM PARTIALSETTLEMENT 
	GROUP BY SHIFTID,DIVISION,PhiscalID
) C ON S.Division=C.division AND S.SessionID=C.SHIFTID AND S.PhiscalID=C.PhiscalID
LEFT JOIN
(
	SELECT sum(advance) AMOUNT, DIVISION, [SHIFT], PhiscalID from RMD_ORDERMAIN WHERE STATUS <> 10
	GROUP BY [SHIFT],DIVISION,PhiscalID
) AC ON S.Division = AC.division AND S.SessionID=AC.[SHIFT] AND S.PhiscalID = AC.PhiscalID
WHERE S.DIVISION LIKE @DIVISION AND S.UserID LIKE @USER AND S.TERMINALID LIKE @SALESTERMINAL AND CONVERT(DATE,SessionStartDate) BETWEEN @DATE1 AND @DATE2
GROUP BY S.SESSIONID, S.USERID, S.SessionStartDate, S.SessionEndDate, S.SessionEndUser, S.TerminalID, S.DIVISION, S.PhiscalID,C.AMOUNT, P.AMOUNT, AC.AMOUNT


SELECT SHIFT, DIVISION, A.PHISCALID, TERMINAL, X.*,
IIF(A.VoucherType IN ('SI','TI','RE'), NETAMNT,0) * IIF(A.VoucherType = 'RE',-1,1) TOT,
IIF(A.VoucherType IN ('SR','CN','NR'), NETAMNT,0) SRETURN,
IIF(A.VoucherType IN ('SR','CN','NR','RE'), -1,1) * NETAMNT NET,
IIF(A.VoucherType IN ('SI','TI','NR','RE'), NETAMNT,0) NOS
, IIF(A.VoucherType IN ('SR','CN','NR','RE'), -1, 1) MULTIPLIER INTO #RESULT
FROM RMD_TRNMAIN A JOIN vwDynamicTender X ON A.VCHRNO = X.VCHRNO 
WHERE (TRNDATE > = @DATE1 AND TRNDATE < = @DATE2)
AND DIVISION LIKE @DIVISION AND A.VoucherType IN ('SI','SR','TI','CN','NR','RE')

SET @PaymentSumCols = STUFF((SELECT distinct ',CONVERT(decimal(18,2),SUM(' + QUOTENAME(COLUMN_NAME) + ' * MULTIPLIER)) ' + QUOTENAME(COLUMN_NAME)  FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = 'vwDynamicTender' AND COLUMN_NAME NOT IN ('VCHRNO','DIVISION','PHISCALID', 'COMPANYID','TRANSACTIONID') FOR XML PATH(''), TYPE).value('.', 'NVARCHAR(MAX)') ,1,1,'')

SET @query = N'
SELECT S.SessionID ShiftId, B.NAME AS TNAME, S.UserID, s.SessionStartDate,S.SessionEndDate, S.SessionEndUser,
' + @PaymentSumCols + ',
CONVERT(decimal(18,2),AdvanceCollection) AS AdvanceCollection,
CONVERT(decimal(18,2),SUM(TOT)) AS TOT, 
CONVERT(decimal(18,2),SUM(SRETURN)) AS SRETURN, 
CONVERT(decimal(18,2),SUM(NET)) AS NET, 
CONVERT(decimal(18,2), S.SETTLEMENTAMOUNT) SETTLEMENTAMOUNT
,CONVERT(decimal(18,2),S.PAYOUTAMOUNT) PAYOUTAMOUNT,CONVERT(decimal(18,2),S.CASHDROPAMOUNT) CASHDROPAMOUNT
,CONVERT(decimal(18,2), S.SETTLEMENTAMOUNT + S.PAYOUTAMOUNT + S.CASHDROPAMOUNT - ISNULL(AdvanceCollection,0) - ISNULL(SUM(CASH * MULTIPLIER), 0)) [EXCESSSHORT]
FROM #SETTLEMENT S LEFT JOIN #RESULT A ON A.SHIFT = S.SessionID AND A.DIVISION = S.Division AND A.PhiscalID = S.PhiscalID
LEFT JOIN SALESTERMINAL B ON S.terminalid = B.INITIAL
WHERE ISNULL(B.INITIAL,'''') LIKE ''' + @SALESTERMINAL + '''
GROUP BY s.SessionID,A.TERMINAL, B.NAME, s.UserID, s.SessionStartDate, S.SessionEndDate, s.SessionEndUser, s.SETTLEMENTAMOUNT,S.PAYOUTAMOUNT,S.CASHDROPAMOUNT, S.AdvanceCollection
ORDER BY s.SessionID'

--PRINT @query

EXECUTE(@query)