CREATE OR ALTER  PROC [dbo].[RSP_PendingConsumptionStockDetail]
--DECLARE 
@DIVISION VARCHAR(3),
@Warehouse VARCHAR(50), 
@PhiscalId VARCHAR(10),
@DATE1 DATE,
@DATE2 DATE
AS
--SELECT @DIVISION = 'PKR', @Warehouse = 'Kitchen', @PhiscalId = '81/82', @DATE1 = '1 JAN 2024', @DATE2 = '18 MAR 2026'
SET NOCOUNT ON
IF OBJECT_ID('TEMPDB..#CONSUMPTION') IS NOT NULL DROP TABLE #CONSUMPTION
IF OBJECT_ID('TEMPDB..#STOCK') IS NOT NULL DROP TABLE #STOCK

DECLARE @ItemWiseWarehouse BIT, @AutoConsumeIntermediateRecipe BIT
SELECT @ItemWiseWarehouse = ItemWiseWarehouse, @AutoConsumeIntermediateRecipe = AutoConsumeIntermediateRecipe FROM SETTING

DROP TABLE IF EXISTS #RecipeDetail

SELECT B.RMCODE MCODE, C.MENUCODE ITEMCODE, C.DESCA ITEMNAME, C.BASEUNIT UNIT,  
	SUM((S.RealQty-S.RealQty_In) * B.QTY / A.FACTOR) CONSUMPTION, D.WHOUSE, C.PTYPE
	INTO #RecipeDetail
	FROM vwPendingConsumptionList PC WITH (NOLOCK)
	INNER JOIN SALES_TRNPROD S WITH (NOLOCK) ON S.VCHRNO = PC.VCHRNO 
	INNER JOIN TRNPROD_RECEIPE B WITH (NOLOCK) ON S.MCODE = B.PMCODE AND S.VCHRNO = B.VCHRNO AND S.SNO = B.SNO
	INNER JOIN MENUITEM C ON B.RMCODE = C.MCODE
	INNER JOIN MENUITEM D ON B.PMCODE = D.MCODE
	INNER JOIN vwActiveRecipeMain A ON A.ENO = B.ENO
	WHERE A.FACTOR > 0 AND PC.TRNDATE BETWEEN @DATE1 AND @DATE2 AND S.DIVISION = @DIVISION AND S.PhiscalID = @PhiscalId
	GROUP BY B.RMCODE,C.MENUCODE, C.DESCA, C.BASEUNIT, D.WHOUSE, C.PTYPE

SELECT MCODE, ITEMCODE,ITEMNAME, UNIT, SUM(CONSUMPTION) CONSUMPTION, WHOUSE
INTO #CONSUMPTION
FROM
(
	SELECT * FROM #RecipeDetail WHERE PTYPE NOT IN (2,5) OR @AutoConsumeIntermediateRecipe = 0
	UNION ALL 
	SELECT B.RMCODE, C.MENUCODE ITEMCODE, C.DESCA ITEMNAME, C.BASEUNIT UNIT, 
	TR.CONSUMPTION * B.QTY / A.FACTOR CONSUMPTION, C.WHOUSE, C.PTYPE
    FROM vwActiveRecipeMain A INNER JOIN vwActiveRecipeProd B ON A.ENO = B.ENO
	JOIN (SELECT MCODE, CONSUMPTION FROM #RecipeDetail WHERE PTYPE IN (2,5)) TR ON A.MCODE = TR.MCODE
    INNER JOIN MENUITEM C ON B.RMCODE = C.MCODE
    INNER JOIN MENUITEM E ON A.MCODE = E.MCODE
    WHERE A.FACTOR > 0 AND @AutoConsumeIntermediateRecipe = 1
) A
GROUP BY MCODE, ITEMCODE,ITEMNAME, UNIT, WHOUSE

SELECT MCODE, WAREHOUSE, SUM(REALQTY_IN - RealQty) STOCK INTO #STOCK FROM RMD_TRNPROD 
WHERE DIVISION = @DIVISION AND PhiscalID = @PhiscalId
GROUP BY MCODE, WAREHOUSE

SELECT C.*, STOCK, IIF(CONSUMPTION>STOCK, CONSUMPTION - STOCK, 0) SHORT FROM #CONSUMPTION C 
LEFT JOIN #STOCK SS ON C.MCODE = SS.MCODE AND IIF(@ItemWiseWarehouse = 1 AND ISNULL(WHOUSE, '') <> '' , WHOUSE , @Warehouse ) = SS.WAREHOUSE
SET NOCOUNT OFF