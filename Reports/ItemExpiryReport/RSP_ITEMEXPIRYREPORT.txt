CREATE OR ALTER PROCEDURE [dbo].[RSP_ITEMEXPIRYREPORT] 
--DECLARE
	@DATE1 DATETIME,
	@DATE2 DATETIME,
	@DIVISION VARCHAR(3) = '%',
	@WAREHOUSE VARCHAR(100) = '%',
	@MENUCAT VARCHAR(100) ='%',
	@SUPPLIER_ACID VARCHAR(25)='%',
	@MGROUP VARCHAR(25) = '%',
	@GROUP VARCHAR(25)='%',
	@FYID VARCHAR(10) = '',
	@CHK_OnlyExpTracked TINYINT = 0,               --CHK_OnlyExpTracked:1:0
	@OPT_REPORTTYPE TINYINT = 0						--NearExpiry:0,Expired:1,All:2
AS
--SET @DATE1 = '2022-05-01'; SET @DATE2 = '2022-07-16'; SET @WAREHOUSE = '%'; SET @DIVISION = '%'; SET @FYID = '78/79'
--DECLARE @DATE DATETIME = '03-27-18',@PATH VARCHAR(200) ='%',@PHISCALID VARCHAR(20)='%',@ITEM VARCHAR(25)= 'M28744P',@DETAIL TINYINT = 0

SET NOCOUNT ON

IF @FYID = ''
 SELECT @FYID = DBO.GetPhiscalID()

DECLARE @CURRENTDATE DATE
SET @CURRENTDATE = GETDATE()

--TEMPORARY TABLE FOR STOCK VALUATION DETAIL RECORD
----------------------------------------------------
IF OBJECT_ID('TEMPDB..#DATA') IS NOT NULL DROP TABLE #DATA
IF OBJECT_ID('TEMPDB..#MENUITEM') IS NOT NULL DROP TABLE #MENUITEM
IF OBJECT_ID('TEMPDB..#STOCK') IS NOT NULL DROP TABLE #STOCK

IF OBJECT_ID('TEMPDB..#RESULT') IS NOT NULL DROP TABLE #RESULT
CREATE TABLE #RESULT (VCHRNO VARCHAR(25),TRNDATE DATETIME,MCODE VARCHAR(25),PQTY NUMERIC(18,6),QTY NUMERIC(18,6),RATE NUMERIC(18,6),AMOUNT NUMERIC(18,6), EXPDATE DATETIME)

SELECT MCODE, MENUCODE, DESCA INTO #MENUITEM FROM MENUITEM B WHERE (@CHK_OnlyExpTracked = 0 OR B.REQEXPDATE = 1) AND B.PTYPE < 10 AND ISNULL(B.MCAT,'') LIKE @MENUCAT AND ISNULL(B.MGROUP,'') LIKE @MGROUP AND ISNULL(B.SUPCODE,'') LIKE @SUPPLIER_ACID

--GETTING STOCK QTY
--------------------

SELECT A.* INTO #STOCK FROM
(
	SELECT A.MCODE,SUM(REALQTY_IN-REALQTY) AS STOCKQTY FROM RMD_TRNPROD_FN(0, @FYID) A
	INNER JOIN RMD_TRNMAIN_FN(0, @FYID) B ON A.VCHRNO= B.VCHRNO
	WHERE A.DIVISION LIKE @DIVISION AND B.TRNDATE <= @DATE2
	GROUP BY A.MCODE  HAVING SUM(REALQTY_IN-REALQTY) <> 0
) A
INNER JOIN #MENUITEM B ON A.MCODE=B.MCODE

--SELECT * from #stock order by mcode

--Get Inward Voucher records to Get Expiry Information
-------------------------------

SELECT A.VCHRNO,A.TRNDATE,C.MCODE, A.EXPDATE, ISNULL(QTY,0) QTY,ISNULL(RATE,0) RATE,ISNULL(C.STOCKQTY,0) STOCKQTY INTO #DATA FROM #STOCK C 
JOIN 
(
	SELECT A.* FROM
	(
		SELECT A.VCHRNO,B.TRNDATE,A.MCODE,A.EXPDATE, SUM(REALQTY_IN) QTY,SUM((CASE WHEN ISNULL(A.CRATE,0) = 0 THEN ISNULL(RATE,0) ELSE ISNULL(A.CRATE,0) END) * REALQTY_IN)/sum(REALQTY_IN) AS RATE 
		FROM  RMD_TRNPROD_FN(0, @FYID) A 
		INNER JOIN RMD_TRNMAIN_FN(0, @FYID) B ON A.VCHRNO=B.VCHRNO 		
		WHERE  A.DIVISION LIKE @DIVISION AND A.VoucherType IN ('PI','OP','TR') AND ISNULL(REALQTY_IN,0) <> 0 AND B.TRNDATE <= @DATE2
		AND ISNULL(A.WAREHOUSE,'') IN (SELECT NAME FROM RMD_WAREHOUSE WHERE ISNULL(IsAdjustment,0) =0 AND ISNULL(ISVIRTUAL,0)=0 )
		GROUP BY A.VCHRNO,B.TRNDATE,A.MCODE, A.EXPDATE
		HAVING SUM(REALQTY_IN) > 0
	) A
	INNER JOIN #MENUITEM M ON A.MCODE = M.MCODE
) AS A ON C.MCODE=A.MCODE 

--SELECT * FROM #data --WHERE MCODE = 'M12266' order by mcode

--FIFO Processing to get expiry date of Remaining stock
-----------------------------------------	
	
INSERT INTO #RESULT(VCHRNO,TRNDATE,MCODE,QTY,RATE,AMOUNT,PQTY, EXPDATE)
SELECT VCHRNO, TRNDATE, MCODE, STKQTY, RATE, STKQTY * RATE AMOUNT, PQTY, EXPDATE FROM
(
	SELECT VCHRNO, TRNDATE, QTY PQTY, RATE, IIF(STOCKQTY>=CPQTY, QTY, STOCKQTY - ISNULL(LAG(CPQTY) OVER (PARTITION BY MCODE ORDER BY TRNDATE DESC, VCHRNO DESC),0)) STKQTY, IIF(STOCKQTY>=CPQTY, QTY, CPQTY-STOCKQTY) * RATE AMOUNT, NULL VRATE, A.MCODE, 'B' FLG, EXPDATE FROM 
	(
		SELECT VCHRNO, TRNDATE, QTY, RATE, STOCKQTY, SUM(QTY) OVER (PARTITION BY MCODE ORDER BY TRNDATE DESC,VCHRNO DESC) CPQTY, MCODE, EXPDATE FROM #DATA
	)A 
) B WHERE STKQTY > 0


IF @OPT_REPORTTYPE = 0	-- GOING TO EXPIRED REPORT
	SET @DATE1 = @CURRENTDATE
ELSE IF @OPT_REPORTTYPE = 1	-- ALREADY EXPIRED REPORT
	SET @DATE2 = @CURRENTDATE

--RESULT OUTPUT
SELECT B.MENUCODE ItemCode, B.DESCA ItemName, A.VCHRNO,A.TRNDATE,A.PQTY InQty, A.RATE, A.QTY STKQTY, AMOUNT,A.MCODE, EXPDATE FROM #RESULT A 
INNER JOIN MENUITEM B ON A.MCODE = B.MCODE 
WHERE EXPDATE IS NOT NULL AND EXPDATE BETWEEN @DATE1 AND @DATE2
ORDER BY A.EXPDATE

DROP TABLE #RESULT
DROP TABLE #STOCK
DROP TABLE #DATA