CREATE OR ALTER PROC OSP_ValidateStockEdit
--DECLARE
@JSON VARCHAR(MAX),
@VCHRNO VARCHAR(50)
AS
--SELECT @JSON = '[{"MCODE":"M10001","BC":"8904063200204","BATCH":null,"Quantity":10.000,"WAREHOUSE":"DASBIGHA"}]', @VCHRNO = 'PI10-MMD-79/80'

DECLARE @BCStock BIT
DECLARE @BatchStock BIT

SELECT @BCStock = CONVERT(BIT,COALESCE(EnableBarcodeDetails,0)), @BatchStock = EnableBatch FROM SETTING

--INSERT EditableStock INTO TEMP TABLE
DECLARE @EditableStock TABLE(MCODE VARCHAR(50), BC VARCHAR(50), BATCH VARCHAR(50), InvoiceQty NUMERIC(18,3),InvoiceQtyOut NUMERIC(18,3), WAREHOUSE VARCHAR(50), TotalInWardStock NUMERIC(18,3), TotalOutWardStock NUMERIC(18,3), EditableQty NUMERIC(18,3))
INSERT INTO @EditableStock
EXEC OSP_GetEditableStock @VCHRNO --'PI1-MMM-78/79'

--SELECT * FROM @EditableStock

--INSERT NEW DATA INTO ANOTHER TEMP TABLE
SELECT D.MCODE, IIF(@BCStock=1 OR MI.PTYPE = 4, COALESCE(BC,''), '') BC, IIF(@BatchStock = 1, COALESCE(D.BATCH,''), '') BATCH, D.WAREHOUSE, SUM(Quantity) Quantity INTO #NewData FROM
 OPENJSON(@JSON) A
	CROSS APPLY OPENJSON(VALUE) 
	WITH 
	(   
        MCODE varchar(50) '$.MCODE',
		BC varchar(50) '$.BC',
		BATCH varchar(50) '$.BATCH',
		WAREHOUSE VARCHAR(50) '$.WAREHOUSE',
		Quantity NUMERIC(18,3) '$.Quantity'
	) D
	JOIN MENUITEM MI ON D.MCODE = MI.MCODE
	GROUP BY D.MCODE, D.BC, D.BATCH, D.WAREHOUSE, MI.PTYPE

--CHECK IF CHANGES MADE IS VALID EDIT
--FOR STOCK IN TRANSACTION VALID = ORIGINAL_QTY - NEW_QTY <= EDITABLE_QTY
--FOR STOCK OUT TRANSACTION VALID = NEW_QTY - ORIGINAL_QTY <= EDITABLE_QTY
SELECT E.*, COALESCE(D.Quantity, 0) NewQty, 
IIF(E.InvoiceQty > 0,
IIF(COALESCE(E.InvoiceQty, COALESCE(D.QUANTITY,0)) - COALESCE(D.Quantity, 0) <= COALESCE(E.EditableQty,0) , 1 , 0),
IIF(COALESCE(D.Quantity, 0) - COALESCE(E.InvoiceQtyOut, COALESCE(D.QUANTITY,0)) <= COALESCE(E.EditableQty,0), 1, 0)) IsValidEdit 
FROM @EditableStock E 
RIGHT JOIN #NewData D ON E.MCODE = D.MCODE AND E.BC = D.BC AND E.BATCH = D.BATCH AND E.WAREHOUSE = D.WAREHOUSE

DROP TABLE #NewData